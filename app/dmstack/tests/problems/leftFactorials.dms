// Copyright 14-Oct-2019 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

100000000 dg =
8 ndg =
25 len =

( n =; N =
  0 len lst,make : R =

  0 // carry
  len
  ( i =
    N i lst,get n * + : dg : int,div
      rem =
      R : i : rem : lst,set
  )
  for
  pop
  R
) mul =>

( @+ll N =; M =
  0 len lst,make : R =

  0 // carry
  len
  ( i =
    N i lst,get M i lst,get + + : r =
    r dg >=
    ( R : i : r dg - : lst,set; 1)
    ( R : i : r : lst,set; 0)
    elif
  )
  for
  pop
  R
@-l) add =>

( @+i n =
  0 len lst,make; 0 1 lst,set+; wrap,ref R =
  (1 : n 1 +) (n =; R (n mul) ^^) for
  R >>
@-l) fact =>

( @+i n =

  0 len lst,make wrap,ref R =
  n
  (
    (0 n)
    ( k =
      R (k fact add) ^^
    )
    for
  )
  if
  R >>
@-l) lfact =>

( @+l:: N =
  N lst,reverse
  0 wrap,ref : Ix =
  (Ix >> len < (N : Ix >> : lst,get : 0 ==) (0) elif) (Ix (1 +) ^^) while
  Ix >> len ==
  ( "0")
  (
    N Ix >> lst,get : toStr
    (Ix >> 1 + : len)
    ( i =
      N i lst,get : toStr; (dup str,len ndg <) ("0" swap +) while
      +
    )
    for
  )
  elif
@-s) toS =>

10 (i =; "!${i} = ${i lfact toS}" sys,println) for
(10 : 115 : 10) (i =; "!${i} = ${i lfact toS}" sys,println) for

empty? assert
